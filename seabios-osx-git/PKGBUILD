# Maintainer: fredbezies <fredbezies at gmail.com>

pkgname=seabios-osx-git
pkgver=rel.1.7.4.r61.gb62632a
pkgrel=1
pkgdesc="A 16-bit x86 bios - git version - to be used with qemu-git"
arch=(any)
license=('GPL3' 'LGPL3')
url="http://www.coreboot.org/SeaBIOS"
makedepends=('iasl' 'python2')
source=('git://git.seabios.org/seabios.git'
        'http://www.contrib.andrew.cmu.edu/~somlo/OSXKVM/seabios-dmi-cumulative-20140204.patch')
options=(!strip)
md5sums=('SKIP'
         '14ed1cec779ba2b617effb088cb49e45')
provides=('seabios')
conflicts=('seabios')

_gitname="seabios"

prepare() {
  cd "${srcdir}/$_gitname"
  patch -Np1 -i "${srcdir}/seabios-dmi-cumulative-20140204.patch" || return 1
}

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build()
{
    cd "${srcdir}/$_gitname"
    sed -i -e 's/python/python2/g' Makefile
    make
}

package()
{
    install -D -m644  "${srcdir}/$_gitname/out/bios.bin" "${pkgdir}/usr/share/qemu/bios.bin"
}

