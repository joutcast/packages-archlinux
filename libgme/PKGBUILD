# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=libgme
pkgver=0.6.0
pkgrel=3jkl
pkgdesc="Video game music file emulation/playback library"
url="http://game-music-emu.googlecode.com/"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'zlib')
makedepends=('cmake')
source=("ftp://ftp.archlinux.org/other/game-music-emu/game-music-emu-${pkgver}.tar.xz"
        "enable-zlib.patch")
md5sums=('b72bc337ea4bd6dab7f0288ed4cd598e'
         '17700d4886d29d93b5b43e4fa5eeb8af')

build() {
  cd "${srcdir}/game-music-emu-${pkgver}"
  patch -p1 -i "${srcdir}/enable-zlib.patch"
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/game-music-emu-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
