#Maintainer: John K. Luebs <jkluebs@luebsphoto.com>

pkgname=dkms-virtualbox
pkgver=4.1.12
pkgrel=1
pkgdesc="Kernel modules (DKMS) for VirtualBox"
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
depends=('dkms')
provides=("virtualbox-modules")
conflicts=("virtualbox-modules")
install=${pkgname}.install
makedepends=('libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2' 'libpng' 'libidl2' 'xalan-c' 'sdl' 'linux-headers')
[[ $CARCH == "x86_64" ]] && makedepends=("${makedepends[@]}" 'gcc-multilib' 'lib32-glibc')
options=(!strip)
source=(http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}.tar.bz2
         LocalConfig.kmk dkms.conf)
sha1sums=('4cda0678fa44ff53af87dbcd9375a43720c69d3f'
          '881ee7b1252cd93a4837472fd9088648de3d5840'
          '7c316860425353b1484298526dc75389b1e847c6')

build() {
    cd "$srcdir/VirtualBox-${pkgver}"

    cp "$srcdir/LocalConfig.kmk" .

    ./configure \
        --with-linux=/usr/src/linux-${_kernver} \
        --disable-java \
        --disable-docs \
        --disable-xpcom \
        --disable-python \
        --disable-sdl-ttf \
        --disable-alsa \
        --disable-pulse \
        --disable-dbus  \
        --disable-opengl \
        --build-headless \
        --nofatal
    source ./env.sh
    kmk all
}

package() {
    source "$srcdir/VirtualBox-${pkgver}/env.sh"
    cd "$srcdir/VirtualBox-${pkgver}/out/linux.$BUILD_PLATFORM_ARCH/release/bin"

    mkdir -p "${pkgdir}/usr/src"
    cp -a src "${pkgdir}/usr/src/vboxhost-${pkgver}"
    cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/vboxhost-${pkgver}"
}
