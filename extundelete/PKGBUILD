# $Id: PKGBUILD 66632 2012-02-26 16:13:47Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: snuo

pkgname=extundelete
pkgver=0.2.0
pkgrel=3jkl
pkgdesc="Utility for recovering deleted files from ext2, ext3 or ext4 partitions by parsing the journal"
arch=('i686' 'x86_64')
url="http://extundelete.sourceforge.net"
license=('GPL')
makedepends=('e2fsprogs')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2 \
        $pkgname-build.patch
        $pkgname-inode-struct.patch)
sha256sums=('0f6a3004d8ddcafe63059a104c174f753c7fc95455081f3ff5aaf3b8c100f97b'
            '6e59a025b98838f43146a9309f5c84b09ca8a314e90a6c9f68837d23ee4d9443'
            '770fda336064f1bb94c96001844fac62bcc7e98547ed956e4a3a973f64100dcf')

build() {
  cd ${srcdir}/$pkgname-$pkgver

#http://patch-tracker.debian.org/package/extundelete/0.2.0-2
  patch -Np1 -i ${srcdir}/$pkgname-build.patch
  patch -Np1 -i ${srcdir}/$pkgname-inode-struct.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
